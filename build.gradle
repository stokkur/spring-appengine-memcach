buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.4'
    }
}
plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'io.spring.dependency-management'

group 'net.stokkur.cloud'
version '0.1-BETA'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

task createPom << {
    pom {
        project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("build/libs/" + createName() + ".pom")
}

bintray {
    user = bintrayUser
    key = bintrayKey
    filesSpec {
        from 'build/libs'
        into createPath()
    }

    pkg {
        repo = 'stokkur'
        name = 'appengine-memcache-spring'
        userOrg = 'stokkur-software'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/stokkur/spring-appengine-memcach.git'
    }
}

def createPath() {
    group.replaceAll('\\.','/') + '/' + rootProject.name + '/' + version
}

def createName() {
    return rootProject.name + '-' + version
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileJava.dependsOn(processResources)

dependencies {
    compile 'com.google.appengine:appengine-api-1.0-sdk:+'
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile group: 'javax.cache', name: 'cache-api', version: '1.0.0'
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    testCompile('org.springframework.boot:spring-boot-starter-test')
}
dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}
